//---------------Old Local Version--------------------
//apply plugin: 'groovy'
//apply plugin: 'maven'
//
//dependencies {
//    implementation fileTree(dir: 'libs', include: ['*.jar'])
//
//    implementation gradleApi()
//    implementation localGroovy()
//
//    implementation 'com.android.tools.build:gradle:4.2.2'
//
//    //ASM相关依赖
//    implementation 'org.ow2.asm:asm:7.1'
//    implementation 'org.ow2.asm:asm-commons:7.1'
//}
//
//group='appplugin'
//version='1.0.0'
//
//uploadArchives {
//    repositories {
//        mavenDeployer {
//            //本地的Maven地址设置
//            repository(url: uri('../apppluginrepo'))
//        }
//    }
//}


//---------------New Local Version--------------------
//plugins {
//    id 'groovy'
//    id 'maven-publish'
//    id 'signing'
//}
//
//
//dependencies {
//    implementation fileTree(dir: 'libs', include: ['*.jar'])
//
//    implementation gradleApi()
//    implementation localGroovy()
//
//    implementation 'com.android.tools.build:gradle:4.2.2'
//
//    //ASM相关依赖
//    implementation 'org.ow2.asm:asm:7.1'
//    implementation 'org.ow2.asm:asm-commons:7.1'
//}
//
//publishing {
//    publications {
//        //配置POM
//        release(MavenPublication) {
//            groupId = 'com.stew.appplugin'
//            artifactId = 'lifeplugin'
//            version = '1.0.0'
//
//            from components.java
//        }
//    }
//
//    repositories {
//        maven {
//            url "../apppluginrepo"
//        }
//    }
//}


//---------------Remote Version--------------------

// add maven-publish and signing gradle plugin
plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}
dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation gradleApi()
    implementation localGroovy()

    implementation 'com.android.tools.build:gradle:4.2.2'

    //ASM相关依赖
    implementation 'org.ow2.asm:asm:7.1'
    implementation 'org.ow2.asm:asm-commons:7.1'
}
group "io.github.stewforani"
version "1.0.0"

java {
    withJavadocJar()
    withSourcesJar()
}
// add publish script
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId "lifemonitor"
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Activity Life Log'
                description = 'get log from activity execute oncreate'
                url = 'https://github.com/stewForAni/lifemonitor'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id = 'stewForAni'
                        name = 'stew'
                        email = 'stewForAni@gmail.com'
                    }
                }

                scm {
                    connection = 'https://github.com/stewForAni/lifemonitor.git'
                    developerConnection = 'https://github.com/stewForAni/lifemonitor.git'
                    url = 'https://github.com/stewForAni/lifemonitor'
                }

            }
        }
    }
    repositories {
        maven {
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username ossrhUsername // ossrhUsername is your sonatype username
                password ossrhPassword // ossrhUsername is your sonatype password
            }
        }
    }
}

// signing, this need key, secret, we put it into gradle.properties
signing {
    sign publishing.publications.mavenJava
}
